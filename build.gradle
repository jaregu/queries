buildscript {
     repositories {
         jcenter()
     }
     dependencies {
         classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
     }
}

plugins {
	id "com.gradle.build-scan" version "2.3"
}

apply plugin: 'com.bmuschko.nexus'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'eclipse-wtp'
 
repositories {
	mavenCentral()
	// gradlePluginPortal()
	jcenter()
	maven { url "https://maven.atlassian.com/3rdparty/" }
}

buildScan {
	termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
}

[
	compileJava,
	compileTestJava
]*.options*.encoding = 'UTF-8'

sourceCompatibility = 11

version = '1.4.1'
archivesBaseName = "queries"
group = "com.jaregu"

eclipseJdt.doLast {
	File f = file('.settings/org.eclipse.core.resources.prefs')
	f.write('eclipse.preferences.version=1\n')
	f.append('encoding/<project>=utf-8')
}

configurations {
	example
	exampleCompile.extendsFrom testCompile
	exampleRuntime.extendsFrom testRuntime
}

sourceSets {
	example {
		java { srcDir 'src/example/java' }
		resources { srcDir 'src/example/resources' }
	}
}

eclipse {
	classpath {
		plusConfigurations += [configurations.example]
	}
}

dependencies {
	compile 'org.slf4j:slf4j-api:1.7.25'
	
	compileOnly 'com.fasterxml.jackson.core:jackson-annotations:2.15.2'
	
	// for caching extension
	compileOnly 'com.github.ben-manes.caffeine:caffeine:3.1.7'

	// for metrics extension
	compileOnly 'io.dropwizard.metrics5:metrics-core:5.0.0-rc17'

	// for guice extension
	compileOnly 'com.google.inject:guice:7.0.0'
	
	// for hikari extension
	compileOnly 'com.zaxxer:HikariCP:5.0.1'
	
	// for dalesbred extension
	compileOnly 'org.dalesbred:dalesbred:1.3.5'

	testCompile 'org.mockito:mockito-core:5.4.0'
	testCompile platform('org.junit:junit-bom:5.10.0')
	testCompile 'org.junit.jupiter:junit-jupiter'
	testCompile 'org.junit.vintage:junit-vintage-engine'
	testCompile 'org.assertj:assertj-core:3.24.2'
	testCompile 'org.dalesbred:dalesbred:1.3.5'

	example "com.github.ben-manes.caffeine:caffeine:2.+"

	//logging
	example 'ch.qos.logback:logback-classic:1.2.+'
	example 'org.codehaus.groovy:groovy-all:2.4.13'

	//db
	example "org.hsqldb:hsqldb:2.4.+"
	example 'org.mariadb.jdbc:mariadb-java-client:1.5.+'
	example group: 'com.oracle', name: 'ojdbc6', version: '12.1.0.1-atlassian-hosted'

	example 'com.zaxxer:HikariCP:5.0.1'
	example 'org.dalesbred:dalesbred:1.3.5'
	example 'org.assertj:assertj-core:3.24.2'
	example platform('org.junit:junit-bom:5.10.0')
	example 'org.junit.jupiter:junit-jupiter'
	example 'org.junit.vintage:junit-vintage-engine'
	
	def lombokVersion = '1.18.28'
	annotationProcessor 'org.projectlombok:lombok:' + lombokVersion
	compileOnly 'org.projectlombok:lombok:' + lombokVersion
    testAnnotationProcessor("org.projectlombok:lombok:$lombokVersion")
    testCompileOnly("org.projectlombok:lombok:$lombokVersion")
}

jar {
	manifest {
		attributes 'Implementation-Title': 'Jaregu Queries', 'Implementation-Version': version
	}
}

modifyPom {
     project {
         name 'Jaregu Queries'
         description 'Java based SQL templating project. Store your queries in *.sql files and build queries for execution. Supports simple expressions and conditional clauses and interface proxying for java-sql bridge.'
         url 'https://github.com/jaregu/queries'
         inceptionYear '2017'

         scm {
             url 'https://github.com/jaregu/queries'
             connection 'scm:https://jaregu@github.com/jaregu/queries.git'
             developerConnection 'scm:git://github.com/jaregu/queries.git'
         }

         licenses {
             license {
                 name 'The Apache Software License, Version 2.0'
                 url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                 distribution 'repo'
             }
         }

         developers {
             developer {
                 id 'mme-private'
                 name 'MƒÅrcis Meijers'
                 email 'marcis.meijers@gmail.com'
                 roles {
                     role 'owner'
                     role 'developer'
                 }
             }
         }
     }
 }
 
 extraArchive {
     sources = true
     tests = true
     javadoc = true
 }